import org.gradle.internal.jvm.Jvm

evaluationDependsOn(':lib')

apply plugin: 'com.android.application'

repositories {
    maven {
        url 'http://dl.bintray.com/alexanderr/maven'
    }
}

android {
    compileSdkVersion 'android-24'
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId 'net.sf.fakenames.aidl2.demo2'
        minSdkVersion 18
        targetSdkVersion 24
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        jackOptions {
            enabled = true
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ 'aidl2_log_to_file' : 'true' ]

                includeCompileClasspath = true
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

android.applicationVariants.all { variant ->
    def buildTypeName = variant.buildType.name.capitalize()
    def productFlavorNames = variant.productFlavors.collect { it.name.capitalize() } ?: ['']
    def productFlavorName = productFlavorNames.join('')
    def variationName = "${productFlavorName}${buildTypeName}"

    tasks.withType(Test) { task ->
        if (task.name.contains(variationName)) {
            def variationPath = variant.buildType.name;

            if (productFlavorName) {
                variationPath = "${productFlavorName.uncapitalize()}/$variationPath"
            }

            def copyTestResourcesTask = project.tasks.create("copyTest${variationName}Resources", Copy) {
                it.from("${projectDir}/src/test/resources")
                it.into("${buildDir}/intermediates/classes/test/${variationPath}")
            }

            task.dependsOn(copyTestResourcesTask)
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'org.checkerframework:checker-qual:2.1.3'
    //compile 'org.checkerframework:checker:2.1.3'

    testCompile 'junit:junit:4.12'

    if (project.properties.uploadInitiated || !project.hasProperty('theTimeHasCometh')) {
        annotationProcessor project(':lib')
        compile project(':lib')
    } else {
        annotationProcessor ('net.sf.aidl2:compiler:0.+') { changing = true }
        compile ('net.sf.aidl2:compiler:0.+') { changing = true }
    }

    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.google.truth:truth:0.28'

    testCompile 'junit:junit:4.12'
    testCompile 'com.google.testing.compile:compile-testing:0.9'
    testCompile 'com.google.truth:truth:0.28'
    testCompile files(Jvm.current().getRuntimeJar())
    testCompile files(Jvm.current().getToolsJar())
}
